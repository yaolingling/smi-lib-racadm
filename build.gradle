apply plugin: 'com.github.jacobono.jaxb'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.sonarqube'
apply plugin: 'com.github.hierynomus.license'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'signing'
apply plugin: 'maven'
//apply plugin: 'nebula.lint'

sourceCompatibility = 1.8
version = "${version}"
group = 'com.dell.isg.smi'
archivesBaseName = "racadmlib"
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

buildscript {
   repositories {
    mavenLocal()
	maven {
		url "${artifactory_contextUrl}/${plugin_repo}"
		url "https://plugins.gradle.org/m2/"
	}
  }
  dependencies {
       classpath "io.spring.gradle:dependency-management-plugin:1.0.2.RELEASE"
       classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.4.15"
       classpath 'com.github.jacobono:gradle-jaxb-plugin:1.3.6'
       classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.2.1"
       classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.13.1"
       //classpath 'com.netflix.nebula:gradle-lint-plugin:latest.release'
  }
}

//gradleLint {
  //rules  = ['dependency-parentheses','overridden-dependency-version','duplicate-dependency-class', 'all-dependency','unused-dependency']
//}

sonarqube {
    properties {
        property "sonar.projectName", "rackhd :: smi-lib-racadm"
        property "sonar.projectKey", "${group}:racadmlib"
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Dell RacAdm jar',
                   'Implementation-Version': version
    }
}

if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

javadoc {
    options {
         encoding = 'windows-1251'
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}


repositories {
	mavenLocal()
	maven { url "${artifactory_contextUrl}/libs-snapshot" }
	maven { url "${artifactory_contextUrl}/libs-release" }
	maven { url "https://repo.maven.apache.org/maven2" }
}

dependencies {
        compile 'com.dell.isg.smi:commons-elm:1.0.76'
        compile 'com.dell.isg.smi:commons-utilities:1.0.32'
        compile 'com.dell.isg.smi:wiseman:1.0.5'
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
        compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'
        compile group: 'commons-io', name: 'commons-io', version: '2.5'
        jaxb 'com.sun.xml.bind:jaxb-xjc:2.2.7-b41'
        jaxb 'com.sun.xml.bind:jaxb-impl:2.2.7-b41'
        jaxb 'javax.xml.bind:jaxb-api:2.2.7'
        jaxb "org.jvnet.jaxb2_commons:jaxb2-basics-ant:0.6.5"
  	jaxb "org.jvnet.jaxb2_commons:jaxb2-basics:0.6.4"
  	jaxb "org.jvnet.jaxb2_commons:jaxb2-basics-annotate:0.6.4"
}

jaxb {
  xsdDir = "src/main/resources/racadm"

  xjc {
     taskClassname      = "org.jvnet.jaxb2_commons.xjc.XJC2Task"
     destinationDir 	= "src/main/java"
     generatePackage    = "com.dell.isg.smi.common.protocol.command.chassis.entity"
     args               = ["-Xinheritance", "-Xannotate"]
  }
}

test {
    systemProperties 'property': 'value'
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
       mavenDeployer
	   {
		beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
			
		repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
			authentication(userName: ossrhUsername, password: ossrhPassword)
		}

		snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
			authentication(userName: ossrhUsername, password: ossrhPassword)
		}
	  
	        pom.project {
				artifactId = "racadmlib"
				packaging 'jar'
				url 'https://www.github.com/rackhd/smi-lib-racadm'
				name 'SMI RacAdm Library'
				description 'A Jar library with implementation logic for interfacing with Dell Systems using the RacAdm protocol/endpont.'
				inceptionYear '2017'
				
				licenses {
					license {
						name 'The Apache Software License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution 'repo'
					}
				}
				
				scm {
				  connection 'scm:git:git://www.github.com/rackhd/smi-lib-racadm.git'
				  developerConnection 'scm:git:ssh://www.github.com/rackhd/smi-lib-racadm.git'
				  url 'http://www.github.com/rackhd/smi-lib-racadm'
				}
				
				developers {
					developer {
						id 'michaelsteven'
						name 'Michael Hepfer'
						email 'michael.hepfer@dell.com'
						organization 'Dell EMC Inc.'
						organizationUrl 'http://codedellemc.com'
						}
				}
				
			}
		}
    }
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("artifactoryPublish") }
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication){
            artifact jar
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar
        }
    }
}

license {
        header rootProject.file('license-template')
        includes(["**/*.java", "**/*.properties"])
        strictCheck true
        ignoreFailures true
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
    	ext.name = 'DELL Inc.'
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        defaults{ publications ("mavenJava")}
        repository {
	    // note: username and password being set by bamboo artifactory plugin
            maven = true
        }
    }
    resolve {
        repository {
            repoKey = 'libs-release'
            maven = true
        }
    }
}
